import Mathlib.Probability.Density
import Mathlib.Probability.Notation
import Mathlib.MeasureTheory.Measure.Lebesgue.Basic
import Mathlib.MeasureTheory.Constructions.Prod.Integral
import Mathlib.Analysis.SpecialFunctions.Integrals

open MeasureTheory (MeasureSpace IsProbabilityMeasure pdf.IsUniform Measure)
open ProbabilityTheory

lemma set_integral_toReal_ofReal [MeasureSpace Î±] {s : Set Î±} {f : Î± â†’ â„}
    (hs : MeasurableSet s) (hf : âˆ€ x âˆˆ s, f x â‰¥ 0) :
    âˆ« (x : Î±) in s, ENNReal.toReal (ENNReal.ofReal (f x)) =
    âˆ« (x : Î±) in s, f x := by

  have comp_eq : (fun x => ENNReal.toReal (ENNReal.ofReal (f x))) = (ENNReal.toReal âˆ˜ ENNReal.ofReal âˆ˜ f) := by rfl
  simp_rw [comp_eq]

  have eq_on : Set.EqOn (ENNReal.toReal âˆ˜ ENNReal.ofReal âˆ˜ f) f s := by
    intro x hx
    simp only [Function.comp_apply, ENNReal.toReal_ofReal_eq_iff]
    exact hf x hx

  rw [MeasureTheory.set_integral_congr hs eq_on]

lemma sin_mul_le (l d Î¸ : â„) (hlâ‚ : l â‰¥ 0) (hlâ‚‚ : l â‰¤ d) : Î¸.sin * l â‰¤ d := by
  rw [mul_comm, â† mul_one d]
  apply mul_le_mul_of_le_of_le hlâ‚‚ Î¸.sin_le_one hlâ‚ zero_le_one

lemma neg_sin_mul_le (l d Î¸ : â„) (hlâ‚ : l â‰¥ 0) (hlâ‚‚ : l â‰¤ d) : -(Î¸.sin * l) â‰¤ d := by
  rw [mul_comm, â† mul_one d, mul_comm, â† neg_mul, mul_comm, â†Real.sin_neg]
  apply mul_le_mul_of_le_of_le hlâ‚‚ (-Î¸).sin_le_one hlâ‚ zero_le_one

notation "Ï€" => Real.pi

variable
  /- Probability theory variables. -/
  {Î© : Type*} [MeasureSpace Î©] [IsProbabilityMeasure (â„™ : Measure Î©)]

  /- Buffon's needle variables. -/

  /-
    - `d > 0` is the distance between parallel lines.
    - `l > 0` is the length of the needle.
  -/
  (d l : â„)
  (hd : d > 0)
  (hl : l > 0)

  /-
    `B = (X, Î˜)` is the joint random variable for the x-position and angle of
    the needle. If a line is at `x = 0`, by symmetry, `B` can be a uniform
    random variable over `[-d/2, d/2] Ã— [0, Ï€]`, where `Î¸` is the angle of the
    needle relative to the y-axis.
  -/
  (B : Î© â†’ â„ Ã— â„)
  (hBâ‚˜ : Measurable B)

  /- `B` is uniformly distributed on `[-d/2, d/2] Ã— [0, Ï€]`. -/
  (hB : pdf.IsUniform B ((Set.Icc (-d / 2) (d / 2)) Ã—Ë¢ (Set.Icc 0 Ï€)) â„™)

/--
  Projection of a needle onto the x-axis. The needle's center is at
  x-coordinate `x`, of length `l` and angle `Î¸`. Note, `Î¸` is measured
  relative to the y-axis, that is, a vertical needle has `Î¸ = 0`.
-/
def needle_x_proj (x Î¸ : â„) : Set â„ := Set.Icc (x - Î¸.sin * l / 2) (x + Î¸.sin * l / 2)

/--
  A random variable representing whether the needle crosses a line.

  The line is at `x = 0`, and the needle crosses the line if it's projection
  onto the x-axis contains `0`. This random variable is `1` if the needle
  crosses the line, and `0` otherwise.

  Note: `N : Î© â†’ â„` is the random variable; the definition of `N' : â„ Ã— â„` is
  provided for convenience.
-/
noncomputable def N' (p : â„ Ã— â„) : â„ := Set.indicator (needle_x_proj l p.1 p.2) 1 0
noncomputable def N : Î© â†’ â„ := N' l âˆ˜ B

lemma short_needle_inter_eq (h : l â‰¤ d) (Î¸ : â„) :
    Set.Icc (-d / 2) (d / 2) âˆ© Set.Icc (-Î¸.sin * l / 2) (Î¸.sin * l / 2) =
    Set.Icc (-Î¸.sin * l / 2) (Î¸.sin * l / 2) := by

  apply Set.inter_eq_self_of_subset_right
  apply Set.Icc_subset
  all_goals rw [Set.mem_Icc]

  Â· apply And.intro <;> apply div_le_div_of_le (le_of_lt two_pos) <;> rw [neg_mul]
    Â· exact neg_le_neg (sin_mul_le l d Î¸ hl.le h)
    Â· exact neg_sin_mul_le l d Î¸ hl.le h

  Â· apply And.intro <;> apply div_le_div_of_le (le_of_lt two_pos)
    Â· exact neg_le.mpr (neg_sin_mul_le l d Î¸ hl.le h)
    Â· exact sin_mul_le l d Î¸ hl.le h

abbrev B_range := Set.Icc (-d / 2) (d / 2) Ã—Ë¢ Set.Icc 0 Ï€

lemma B_range_volume : â„™ (B_range d) = ENNReal.ofReal (d * Ï€) := by
  simp_rw [MeasureTheory.Measure.volume_eq_prod, MeasureTheory.Measure.prod_prod, Real.volume_Icc]
  ring_nf
  exact (ENNReal.ofReal_mul hd.le).symm

lemma B_range_nonzero : â„™ (B_range d) â‰  0 := by
  rw [B_range_volume d hd, ne_eq, ENNReal.ofReal_eq_zero, not_le]
  exact mul_pos hd Real.pi_pos

lemma B_range_nontop : â„™ (B_range d) â‰  âŠ¤ := by
  rw [B_range_volume d hd]
  exact ENNReal.ofReal_ne_top

lemma B_range_measurable : MeasurableSet (B_range d) :=
  MeasurableSet.prod measurableSet_Icc measurableSet_Icc

instance instBHasPDF : MeasureTheory.HasPDF B â„™ :=
  MeasureTheory.pdf.IsUniform.hasPDF
    (B_range_measurable d) (B_range_nonzero d hd) (B_range_nontop d hd) hB

lemma N'_measurable : Measurable (N' l) := by
  unfold N'

  apply Measurable.indicator measurable_const
  apply IsClosed.measurableSet
  apply IsClosed.inter

  case' h.hâ‚ =>
    simp only [tsub_le_iff_right, zero_add]
    apply isClosed_le continuous_fst _
  case' h.hâ‚‚ =>
    simp only [â† neg_le_iff_add_nonneg']
    apply isClosed_le _ _

  Â· conv => arg 1; intro p; rw [â† mul_one p.1, â† mul_neg]
    exact Continuous.mul continuous_fst continuous_const

  all_goals
  Â· simp_rw [mul_div_assoc]
    apply Continuous.mul _ continuous_const
    have : (fun (x : â„ Ã— â„) => Real.sin x.2) = Real.sin âˆ˜ Prod.snd := by rfl
    rw [this]
    apply Continuous.comp Real.continuous_sin continuous_snd

lemma N'_strongly_measurable : MeasureTheory.StronglyMeasurable (N' l) := by
  apply stronglyMeasurable_iff_measurable_separable.mpr
  apply And.intro
  Â· exact N'_measurable l
  Â· apply Exists.intro {0, 1}
    have range_finite : Set.Finite ({0, 1} : Set â„) := by
      simp only [Set.mem_singleton_iff, Set.finite_singleton, Set.Finite.insert]
    apply And.intro
    Â· exact Set.Finite.countable range_finite
    Â· rw [IsClosed.closure_eq (Set.Finite.isClosed range_finite)]
      unfold N' Set.range
      rw [Set.subset_def]
      intro x âŸ¨p, hxpâŸ©
      by_cases hp : 0 âˆˆ needle_x_proj l p.1 p.2
      Â· simp_rw [Set.indicator_of_mem hp, Pi.one_apply] at hxp
        apply Or.inr hxp.symm
      Â· simp_rw [Set.indicator_of_not_mem hp] at hxp
        apply Or.inl hxp.symm

lemma N'_integrable_prod :
    MeasureTheory.Integrable (N' l)
      (Measure.prod (Measure.restrict â„™ (Set.Icc (-d / 2) (d / 2))) (Measure.restrict â„™ (Set.Icc 0 Ï€))) := by

  have N'_nonneg p : N' l p â‰¥ 0 := by
    apply Set.indicator_apply_nonneg
    simp only [Pi.one_apply, zero_le_one, implies_true]

  have N'_le_one p : N' l p â‰¤ 1 := by
    unfold N'
    by_cases hp : 0 âˆˆ needle_x_proj l p.1 p.2
    Â· simp_rw [Set.indicator_of_mem hp, Pi.one_apply, le_refl]
    Â· simp_rw [Set.indicator_of_not_mem hp, zero_le_one]

  apply And.intro (N'_strongly_measurable l).aestronglyMeasurable
  Â· apply (MeasureTheory.hasFiniteIntegral_iff_norm (N' l)).mpr
    apply lt_of_le_of_lt
    apply MeasureTheory.lintegral_mono (g := 1)
    Â· simp only [Real.norm_eq_abs, abs_of_nonneg (N'_nonneg _)]
      intro p
      simp only [ENNReal.ofReal_le_one, Pi.one_apply]
      exact N'_le_one p
    simp only [Pi.one_apply, MeasureTheory.lintegral_const, one_mul, MeasureTheory.Measure.prod_restrict]
    rw [MeasureTheory.Measure.restrict_apply MeasurableSet.univ, Set.univ_inter, MeasureTheory.Measure.prod_prod]
    simp_rw [Real.volume_Icc]
    ring_nf
    rw [â† ENNReal.ofReal_mul hd.le]
    exact ENNReal.ofReal_lt_top

lemma buffon_integral :
    ğ”¼[N l B] = (d * Ï€) â»Â¹ *
      âˆ« (Î¸ : â„) in Set.Icc 0 Ï€,
      âˆ« (x : â„) in Set.Icc (-d / 2) (d / 2) âˆ© Set.Icc (-Î¸.sin * l / 2) (Î¸.sin * l / 2), 1 := by
  simp_rw [N, Function.comp_apply]

  rw [
    â† MeasureTheory.integral_map (f := N' l) hBâ‚˜.aemeasurable (N'_strongly_measurable l).aestronglyMeasurable,
    hB,
    MeasureTheory.integral_smul_measure,
    B_range_volume d hd,
    ENNReal.ofReal_inv_of_pos (mul_pos hd Real.pi_pos),
    ENNReal.toReal_ofReal (inv_nonneg.mpr (mul_nonneg hd.le Real.pi_pos.le)),
    smul_eq_mul, mul_eq_mul_left_iff
  ]

  apply Or.inl

  have Real_measure_prod : (â„™ : Measure (â„ Ã— â„)) = Measure.prod â„™ â„™ := rfl

  have : MeasureTheory.IntegrableOn (N' l) (Set.Icc (-d / 2) (d / 2) Ã—Ë¢ Set.Icc 0 Ï€) := by
    apply (MeasureTheory.integrableOn_def _ _ _).mpr
    rw [Real_measure_prod, â† MeasureTheory.Measure.prod_restrict]
    exact N'_integrable_prod d l hd

  rw [
    Real_measure_prod,
    MeasureTheory.set_integral_prod _ this,
    MeasureTheory.integral_integral_swap ?integrable,
  ]

  case integrable =>
    simp_rw [Function.uncurry_def, Prod.mk.eta]
    exact N'_integrable_prod d l hd

  unfold N' needle_x_proj
  simp only [Set.mem_Icc]

  have indicator_eq (x Î¸ : â„) :
    Set.indicator (Set.Icc (x - Î¸.sin * l / 2) (x + Î¸.sin * l / 2)) 1 0 =
    Set.indicator (Set.Icc (-Î¸.sin * l / 2) (Î¸.sin * l / 2)) (1 : â„ â†’ â„) x := by
    simp_rw [Set.indicator, Pi.one_apply, Set.mem_Icc, tsub_le_iff_right, zero_add, neg_mul]

    have :
        x â‰¤ Real.sin Î¸ * l / 2 âˆ§ 0 â‰¤ x + Real.sin Î¸ * l / 2 â†”
        -(Real.sin Î¸ * l) / 2 â‰¤ x âˆ§ x â‰¤ Real.sin Î¸ * l / 2 := by
        rw [neg_div, and_comm, â†tsub_le_iff_right, zero_sub]

    by_cases h : x â‰¤ Real.sin Î¸ * l / 2 âˆ§ 0 â‰¤ x + Real.sin Î¸ * l / 2
    Â· rw [if_pos h, if_pos (this.mp h)]
    Â· rw [if_neg h, if_neg (this.not.mp h)]

  simp_rw [indicator_eq, MeasureTheory.set_integral_indicator measurableSet_Icc, Pi.one_apply]

/--
  Buffon's Needle, the short case (`l â‰¤ d`). The probability of the needle crossing a line equals `(2 * l) / (d * Ï€)`.
-/
theorem buffon_short (h : l â‰¤ d) : â„™[N l B] = (2 * l) * (d * Ï€)â»Â¹ := by
  simp_rw [
    buffon_integral d l hd B hBâ‚˜ hB,
    short_needle_inter_eq d l hl h _,
    MeasureTheory.set_integral_const,
    Real.volume_Icc,
    smul_eq_mul,
    mul_one,
  ]

  rw [mul_comm, mul_eq_mul_right_iff]
  apply Or.inl
  ring_nf

  have : âˆ€ Î¸ âˆˆ Set.Icc 0 Ï€, Î¸.sin * l â‰¥ 0 := by
    intro Î¸ hÎ¸
    exact mul_nonneg (Real.sin_nonneg_of_mem_Icc hÎ¸) hl.le

  rw [set_integral_toReal_ofReal measurableSet_Icc this]

  conv in (_ * l) => rw [â† smul_eq_mul]
  simp_rw [integral_smul_const, smul_eq_mul, mul_comm, mul_eq_mul_left_iff]
  apply Or.inl

  rw [
    MeasureTheory.integral_Icc_eq_integral_Ioc,
    â† intervalIntegral.integral_of_le Real.pi_pos.le,
    integral_sin,
    Real.cos_zero,
    Real.cos_pi
  ]

  ring_nf

lemma min_sin_mul_continuous : Continuous (fun (Î¸ : â„) => min d (Î¸.sin * l)) := by
  apply Continuous.min continuous_const
  apply Continuous.mul Real.continuous_sin continuous_const

lemma integral_min_eq_two_mul : âˆ« Î¸ in (0)..Ï€, min d (Î¸.sin * l) = 2 * âˆ« Î¸ in (0)..Ï€/2, min d (Î¸.sin * l) := by
  rw [â† intervalIntegral.integral_add_adjacent_intervals (b := Ï€ / 2) (c := Ï€)]
  conv => lhs; arg 2; arg 1; intro Î¸; rw [â† neg_neg Î¸, Real.sin_neg]

  simp_rw [
    intervalIntegral.integral_comp_neg fun Î¸ => min d (-Î¸.sin * l),
    â† Real.sin_add_pi,
    intervalIntegral.integral_comp_add_right (fun Î¸ => min d (Î¸.sin * l)),
    add_left_neg,
    (by ring : -(Ï€/2) + Ï€ = Ï€/2),
    two_mul,
  ]

  all_goals
    exact Continuous.intervalIntegrable (min_sin_mul_continuous d l) _ _

lemma integral_zero_to_arcsin_min :
    âˆ« Î¸ in (0)..(d / l).arcsin, min d (Î¸.sin * l) = (1 - (1 - (d / l) ^ 2).sqrt) * l := by
  have : Set.EqOn (fun Î¸ => min d (Î¸.sin * l)) (fun Î¸ => Î¸.sin * l) (Set.uIcc 0 (d / l).arcsin) := by
    intro Î¸ âŸ¨hÎ¸â‚, hÎ¸â‚‚âŸ©
    have arcsin_nonneg : (d / l).arcsin â‰¥ 0 := Real.arcsin_nonneg.mpr (div_nonneg hd.le hl.le)
    simp only [sup_eq_max, inf_eq_min, min_eq_left arcsin_nonneg, max_eq_right arcsin_nonneg] at hÎ¸â‚ hÎ¸â‚‚
    have hÎ¸_mem : Î¸ âˆˆ Set.Ioc (-(Ï€ / 2)) (Ï€ / 2) := by
      apply And.intro
      Â· calc
        -(Ï€ / 2) < 0 := neg_lt_zero.mpr (div_pos Real.pi_pos two_pos)
        _        â‰¤ Î¸ := hÎ¸â‚
      Â· calc
        Î¸ â‰¤ (d / l).arcsin := hÎ¸â‚‚
        _ â‰¤ Ï€ / 2 := (d / l).arcsin_mem_Icc.right

    have : Î¸.sin * l â‰¤ d := (le_div_iff hl).mp ((Real.le_arcsin_iff_sin_le' hÎ¸_mem).mp hÎ¸â‚‚)
    simp_rw [min_eq_right this]

  rw [intervalIntegral.integral_congr this, intervalIntegral.integral_mul_const, integral_sin,
    Real.cos_zero, Real.cos_arcsin]

lemma integral_arcsin_to_pi_div_two_min (h : l â‰¥ d) :
    âˆ« Î¸ in (d / l).arcsin..(Ï€ / 2), min d (Î¸.sin * l) = (Ï€ / 2 - (d / l).arcsin) * d := by

  have : Set.EqOn (fun Î¸ => min d (Î¸.sin * l)) (fun _ => d) (Set.uIcc (d / l).arcsin (Ï€ / 2)) := by
    intro Î¸ âŸ¨hÎ¸â‚, hÎ¸â‚‚âŸ©

    wlog hÎ¸_ne_pi_div_two : Î¸ â‰  Ï€ / 2
    Â· simp only [ne_eq, not_not] at hÎ¸_ne_pi_div_two
      simp only [hÎ¸_ne_pi_div_two, Real.sin_pi_div_two, one_mul, min_eq_left h]

    simp only [sup_eq_max, inf_eq_min, min_eq_left (d / l).arcsin_le_pi_div_two,
      max_eq_right (d / l).arcsin_le_pi_div_two] at hÎ¸â‚ hÎ¸â‚‚

    have hÎ¸_mem : Î¸ âˆˆ Set.Ico (-(Ï€ / 2)) (Ï€ / 2) := by
      apply And.intro
      Â· calc
        -(Ï€ / 2) â‰¤ 0 := neg_nonpos.mpr (div_nonneg Real.pi_pos.le zero_le_two)
        _        â‰¤ (d / l).arcsin := (Real.arcsin_pos.mpr (div_pos hd hl)).le
        _        â‰¤ Î¸ := hÎ¸â‚
      Â· exact lt_of_le_of_ne hÎ¸â‚‚ hÎ¸_ne_pi_div_two

    have : d â‰¤ Î¸.sin * l := (div_le_iff hl).mp ((Real.arcsin_le_iff_le_sin' hÎ¸_mem).mp hÎ¸â‚)
    simp_rw [min_eq_left this]

  rw [intervalIntegral.integral_congr this, intervalIntegral.integral_const, smul_eq_mul]

/--
  Buffon's Needle, the short case (`l â‰¥ d`).
-/
theorem buffon_long (h : l â‰¥ d) :
    â„™[N l B] = (2 * l) / (d * Ï€) - 2 / (d * Ï€) * ((l^2 - d^2).sqrt + d * (d / l).arcsin) + 1 := by

  simp only [
    buffon_integral d l hd B hBâ‚˜ hB, MeasureTheory.integral_const, smul_eq_mul, mul_one,
    MeasurableSet.univ, Measure.restrict_apply, Set.univ_inter, Set.Icc_inter_Icc, Real.volume_Icc,
    sup_eq_max, inf_eq_min, min_div_div_right zero_le_two d, max_div_div_right zero_le_two (-d), div_sub_div_same,
    neg_mul, max_neg_neg, sub_neg_eq_add, â† mul_two, mul_div_cancel (min d (Real.sin _ * l)) two_ne_zero
  ]

  have (Î¸ : â„) (hÎ¸ : Î¸ âˆˆ Set.Icc 0 Ï€) : min d (Î¸.sin * l) â‰¥ 0 := by
    by_cases h : d â‰¤ Î¸.sin * l
    Â· rw [min_eq_left h]; exact hd.le
    Â· rw [min_eq_right (not_le.mp h).le]; exact mul_nonneg (Real.sin_nonneg_of_mem_Icc hÎ¸) hl.le

  rw [set_integral_toReal_ofReal measurableSet_Icc this, MeasureTheory.integral_Icc_eq_integral_Ioc,
    â† intervalIntegral.integral_of_le Real.pi_pos.le, integral_min_eq_two_mul,
    â† intervalIntegral.integral_add_adjacent_intervals (b := (d / l).arcsin),
    integral_zero_to_arcsin_min d l hd hl,
    integral_arcsin_to_pi_div_two_min d l hd hl h]

  have thisâ‚ : (1 - Real.sqrt (1 - (d / l) ^ 2)) * l = l - (l ^ 2 - d ^ 2).sqrt := by
    rw [mul_comm, mul_sub, mul_one, div_pow, one_sub_div, Real.sqrt_div, Real.sqrt_sq hl.le, â† mul_div_assoc, mul_comm,
      mul_div_cancel _ (ne_of_gt hl)]
    Â· rw [sub_nonneg]
      apply sq_le_sq.mpr
      rw [abs_of_pos hd, abs_of_pos hl]
      exact h
    Â· simp only [ne_eq, zero_lt_two, pow_eq_zero_iff]
      exact (ne_of_gt hl)

  have thisâ‚‚ : 2 * d * (Ï€ / 2 - (d / l).arcsin) / (d * Ï€) = 1 - (2 / Ï€) * (d / l).arcsin := by
    rw [mul_sub, sub_div, mul_assoc, â† mul_comm_div, â† mul_assoc, â† mul_comm_div, div_self two_ne_zero, one_mul,
      div_self (ne_of_gt (mul_pos hd Real.pi_pos)), mul_div_assoc, â† mul_comm_div, mul_comm 2,
      mul_div_mul_left _ _ (ne_of_gt hd)]

  have thisâ‚ƒ : 2 * Real.sqrt (l ^ 2 - d ^ 2) / (d * Ï€) = 2 / (d * Ï€) * Real.sqrt (l ^ 2 - d ^ 2) := by ring_nf

  have thisâ‚„ : 2 / Ï€ * d / d = 2 / (d * Ï€) * d := by ring_nf

  conv =>
    lhs
    rw [thisâ‚, inv_mul_eq_div, mul_add, mul_sub, add_div, sub_div, mul_comm (Ï€ / 2 - (d / l).arcsin), â† mul_assoc,
      thisâ‚‚, thisâ‚ƒ, add_sub, add_sub_right_comm, sub_eq_add_neg, sub_eq_add_neg, â† neg_mul,
      â† mul_div_cancel (2 / Ï€) (ne_of_gt hd), thisâ‚„, mul_assoc, â† neg_mul, add_assoc (2 * l / (d * Ï€)) _ _,
      â† mul_add, neg_mul, â† sub_eq_add_neg]

  all_goals
    exact Continuous.intervalIntegrable (min_sin_mul_continuous d l) _ _
